plugins {
    id 'java'
    id 'org.springframework.boot' version '2.5.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
//    id 'com.google.cloud.tools.jib' version '3.1.4'
}

// === Repository config ===
repositories {
    mavenLocal()
    mavenCentral()
}

// === JAVA configuration ===
compileJava {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

// === Dependencies ===
dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.h2database:h2'
    //testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

tasks.withType(Test) {
    useJUnitPlatform()
}

test {
    exclude '**/*IntegrationTest*'
}

task integrationTest(type: Test) {
    group 'verification'
    include '**/*IntegrationTest*'
    it.mustRunAfter test
}

// === Spring Boot Configuration ===
springBoot {
	buildInfo()
}

// === Wrapper Configuration ===
wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

bootBuildImage {
    verboseLogging = false

    //fix version of used images
    builder = 'paketobuildpacks/builder@sha256:4797e9561534e3b628da62201555a3090db077266314e6fa175c2d9187f8429d'
    runImage = 'paketobuildpacks/run@sha256:781ccd109ecd2b800422697313035a2c37378ff26b6fa97d5205566197bcbb79'
    environment = [
            "BP_DEBUG_ENABLED"                     : "true",
            "BPE_DELIM_JAVA_TOOL_OPTIONS"          : " ",
            "BPE_JAVA_TOOL_OPTIONS"                : "-Dfile.encoding=UTF-8",
            "BPE_BPL_SPRING_CLOUD_BINDINGS_ENABLED": "false"
    ]
    publish = false
}

//jib {
//    to {
//        image = "spring-playground"
//    }
//}
