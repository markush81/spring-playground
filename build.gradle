plugins {
    id 'org.springframework.boot' version '2.0.3.RELEASE'
    id "com.palantir.docker" version "0.20.1"
    id "com.palantir.docker-run" version "0.20.1"
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

// === Repository config ===
repositories {
    mavenCentral()
}

// === JAVA configuration ===
sourceCompatibility = JavaVersion.VERSION_1_10
targetCompatibility = JavaVersion.VERSION_1_10

// === Dependencies ===
dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtime 'javax.xml.bind:jaxb-api'
    runtime 'com.h2database:h2'
    //testing
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

test {
    exclude '**/*IntegrationTest*'
}

task integrationTest(type: Test) {
    include '**/*IntegrationTest*'
    it.mustRunAfter test
}

// === Spring Boot Configuration ===
bootJar {
    launchScript()
}

// === Docker ===
dockerRun {
    name bootJar.baseName
    image 'openjdk:10-jdk'
    volumes "${project.buildDir}/libs": '/tmp'
    ports '8080:8080'
    daemonize true
    clean true
    env 'JAVA_OPTS': '-Djava.security.egd=file:/dev/./urandom'
    command "/tmp/${bootJar.archiveName}"
}

// === Wrapper generation ===
task wrapper(type: Wrapper) {
    distributionType = Wrapper.DistributionType.ALL
}
