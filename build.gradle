plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.2'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'com.google.cloud.tools.jib' version '3.3.1'
}

// === Repository config ===
repositories {
    mavenLocal()
    mavenCentral()
}

// === JAVA configuration ===
compileJava {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

configurations {
    otel
}

// === Dependencies ===
dependencies {
    otel 'io.opentelemetry.javaagent:opentelemetry-javaagent'

    implementation platform("io.opentelemetry:opentelemetry-bom:1.22.0")
    implementation platform('io.opentelemetry:opentelemetry-bom-alpha:1.22.0-alpha')

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.kafka:spring-kafka'

    implementation('io.opentelemetry:opentelemetry-api')
    implementation('io.opentelemetry:opentelemetry-sdk-extension-autoconfigure')

    runtimeOnly 'com.h2database:h2'

    //testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

tasks.withType(Test) {
    useJUnitPlatform()
}

test {
    exclude '**/*IntegrationTest*'
}

task integrationTest(type: Test) {
    group 'verification'
    include '**/*IntegrationTest*'
    it.mustRunAfter test
}

// === Spring Boot Configuration ===
springBoot {
    buildInfo {
        properties {
            time = null
        }
    }
}

bootRun {
//    jvmArgs("-javaagent:${configurations.otel.asPath}")
    environment("OTEL_EXPORTER_OTLP_ENDPOINT", "http://localhost:4317")
    environment("OTEL_METRICS_EXPORTER", "otlp")
    environment("OTEL_LOGS_EXPORTER", "otlp")
    environment("OTEL_TRACES_EXPORTER", "otlp")
    environment("OTEL_SERVICE_NAME", "${project.name}")
    environment("OTEL_RESOURCE_ATTRIBUTES", "service.version=${project.version},deployment.environment=dev")
}

// === Wrapper Configuration ===
wrapper {
    distributionType = Wrapper.DistributionType.ALL
}


bootBuildImage {
    verboseLogging = true

    //fix version of used images
    builder = 'paketobuildpacks/builder@sha256:d0c6a5fa9dafc0271a00d7831ad40cc88d0471fd3d8c71c52b41c4045ee8469a'
    runImage = 'paketobuildpacks/run@sha256:ec0263dc98a8c785f4a6750597de8857efd2852fc6544494ff2598426cf7143c'
    environment = [
            "BPE_DELIM_JAVA_TOOL_OPTIONS"       : " ",
            "BPE_APPEND_JAVA_TOOL_OPTIONS"      : "-Dfile.encoding=UTF-8",
            "BP_SPRING_CLOUD_BINDINGS_DISABLED" : "true",
    ]
    publish = false
}

jib {
//    from {
//        platforms {
//            platform {
//                architecture = "arm64"
//                os = "linux"
//            }
//        }
//    }
    to {
        image = "spring-playground"
    }
}
